configurations {
    jacocoRuntime
}

dependencies {
    testRuntime files("$buildDir/testkit")
    jacocoRuntime "org.jacoco:org.jacoco.agent:${jacoco.toolVersion}:runtime"
}

task createTestkitFiles {
    def outputDir = file("$buildDir/testkit")

    inputs.files sourceSets.main.runtimeClasspath
    inputs.files configurations.jacocoRuntime
    outputs.dir outputDir

    doLast {
        outputDir.mkdirs()
        String jacocoPath = configurations.jacocoRuntime.asPath.replace('\\', '/')
        file("$outputDir/testkit-classpath.txt").text = sourceSets.main.runtimeClasspath.join("\n")
        file("$outputDir/testkit-gradle.properties").text = [
            "org.gradle.jvmargs=-javaagent:${jacocoPath}=destfile=$buildDir/jacoco/test.exec",
            "version_spigot = ${version_spigot}",
            "version_bungeecord = ${version_bungeecord}"
        ].join("\n")
    }
}

test {
    dependsOn createTestkitFiles
    testLogging {
        exceptionFormat = 'full'
        showStandardStreams = true
    }
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
        html.destination file("${buildDir}/jacoco/html")
    }
}

check.dependsOn jacocoTestReport
